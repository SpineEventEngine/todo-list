/*
 * Copyright 2016, TeamDev Ltd. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow',
                                                                version: shadowJarVersion
    }
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile group: 'io.spine', name: 'jdbc-rdbms', version: spineVersion
    compile group: 'mysql', name: 'mysql-connector-java', version: mySQLConnectorVersion
    compile project(path: ':server')

    // A Protobuf dependency to generate descriptor for Spine gRPC services.
    // This descriptor is required to configure gRPC services for a Google Cloud Endpoint.
    protobuf group: 'io.spine', name: 'spine-client', version: spineVersion
}

mainClassName = "io.spine.examples.todolist.server.ComputeCloudSqlServer"

// A task to build a `uber-jar`.
shadowJar {

    // To avoid addition of `-all` suffix to the .jar file name.
    classifier = ""

    // Merge multiple property files with a same name into one file.
    //
    // Without merge, the resulting .jar will contain multiple property files
    // (all the files are located in the root of the archive),
    // which generated by Spine (e.g. `known_types.properties`).
    // In this case, only one `known_types.properties` file will be handled,
    // which will cause a wrong behavior.
    mergeServiceFiles {
        path = "*.properties"
    }
}

// Enable Protobuf descriptor generation.
// The descriptor is required to configure a Google Cloud Endpoint.
protobuf {
    generateProtoTasks {
        all().each { final task ->
            task.plugins {
                task.generateDescriptorSet = true

                // The extension `.desc` is not supported in the Endpoints configuration deployment.
                // One of the supported extensions is `.pb`.
                task.descriptorSetOptions.path = "$projectDir/build/descriptors/spine_grpc_services.pb"
            }
        }
    }
}
