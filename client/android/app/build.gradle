/*
 * Copyright 2019, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "io.spine.examples.todolist"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        /*
            As TodoList uses Java 8.
        */
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    packagingOptions {
        /*
            Spine auto-generated .property files.

            There may be several files with such name in the dependencies. All of them are required
            for the framework components to function.

            Note that the resulting merged files may contain duplicate entries. There is currently
            no simple way to avoid those duplicates. Though, their presence does not much harm to
            the application, except for the larger APK file sizes.
        */
        merge 'known_types.properties'
        merge 'validation_rules.properties'
    }

    lintOptions {
        /*
            gRPC uses some `javax` packages that are not included into the Android SDK.
            Nevertheless, this has no visible effect on the application runtime.

            Suppress the compilation failure caused by Android Lint reporting about the packages.
        */
        abortOnError false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android dependencies
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'android.arch.lifecycle:extensions:1.0.0'
    implementation 'com.android.support:design:26.1.0'

    // TodoList dependency
    implementation "io.spine.examples.todolist:client:$spineVersion"

    implementation 'com.google.firebase:firebase-firestore:11.8.0'
}

apply plugin: 'com.google.gms.google-services'

apply plugin: 'kotlin-android-extensions'
